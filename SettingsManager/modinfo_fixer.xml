<?xml version="1.0"?>
<!-- Copyright 2018 FiatAccompli -->
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output method="xml" encoding="utf-8" indent="yes"/>
  <xsl:strip-space elements="*"/>

  <xsl:include href="../modinfo_fixer_base.xml"/>

  <xsl:template match="/Mod/@id">
    <xsl:attribute name="id">
      <xsl:choose>
        <xsl:when test="$release">1cb1beaf-0428-4aad-b11d-e3168429c071</xsl:when>
        <xsl:otherwise>1cbf82d1-e297-4d89-8b64-9bbcd71db2a8</xsl:otherwise>
      </xsl:choose>
    </xsl:attribute>
  </xsl:template>

  <xsl:template match="/Mod/Properties">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
      <xsl:call-template name="AddCompatibleVersions"/>
    </xsl:copy>
    <LocalizedText>
      <xsl:call-template name="GenerateText">
        <xsl:with-param name="identifier">LOC_SETTINGS_MANAGER_MOD_NAME</xsl:with-param>
        <xsl:with-param name="is_mod_name" select="true()"/>
        <xsl:with-param name="base">Settings Manager</xsl:with-param>
      </xsl:call-template>
      <xsl:call-template name="GenerateText">
        <xsl:with-param name="identifier">LOC_SETTINGS_MANAGER_TEASER</xsl:with-param>
        <xsl:with-param name="base">Makes it easy for other mods to declare user configurable settings and gives users a nice UI to change those settings.</xsl:with-param>
      </xsl:call-template>
      <xsl:call-template name="GenerateText">
        <xsl:with-param name="identifier">LOC_SETTINGS_MANAGER_DESCRIPTION</xsl:with-param>
        <xsl:with-param name="base">Makes it easy for other mods to declare user configurable settings and gives users a nice UI to change those settings. Setting values are persisted within game saves and if desired it is easy to save a set of global default values that apply across all saves.</xsl:with-param>
      </xsl:call-template>
    </LocalizedText>
    <xsl:if test="$release">
      <Dependencies>
        <Mod id="c8e215b9-b317-4fae-a9b5-907570575893" title="UI Plugins Framework" />
      </Dependencies>
    </xsl:if>
  </xsl:template>

  <!-- Add Database/mod_settings_default_values.sql to the modinfo.  We don't want that file to actually 
       exist because it would be overwritten every time the mod is updated. -->
  <xsl:template match="/Mod/Files">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
      <xsl:element name="File">Database/mod_settings_default_values.sql</xsl:element>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="/Mod/Files/File[text()='Database/mod_settings_default_values.sql']"/>

  <xsl:template match="UpdateDatabase[@id='ModSettingsUserDefaults']">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
      <File Priority="1">Database/mod_settings_default_values.sql</File>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="UpdateDatabase[@id='ModSettingsUserDefaults']/File[text()='Database/mod_settings_default_values.sql']"/>
</xsl:stylesheet>